group 'cz.ondrajanata'
version '1.0-SNAPSHOT'

apply plugin: 'java'

repositories {
    mavenCentral()
}

buildscript {
    ext {
        springBootVersion = '1.2.3.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("io.spring.gradle:dependency-management-plugin:0.5.6.RELEASE")
    }
}

allprojects {
    group = 'cz.ondrajanata'
    version = '1.0-SNAPSHOT'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    apply plugin: 'idea'

    task wrapper(type: Wrapper) {
        gradleVersion = '2.1'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'spring-boot'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile('org.springframework.boot:spring-boot-starter-test') {
            exclude module: 'commons-logging'
        }

        compile 'org.springframework.boot:spring-boot-starter-web'
        compile 'org.hibernate:hibernate-validator:5.1.3.Final' //JSR303 Validation Support

        compile 'org.apache.commons:commons-lang3:3.4'
    }

    bootRun {
        // Ensures IntelliJ can load resource files
        addResources = false

        def springActiveProfileKey = 'spring.profiles.active';
        def profileKey = 'profile'

        if (project.hasProperty(profileKey)) {
            def activeProfile = project.getProperty(profileKey)
            println("Setting profile to: " + activeProfile)
            systemProperties[springActiveProfileKey] = activeProfile
        }
    }

    task deployGCloud(dependsOn: "bootRepackage") << {
        File copyFrom = file("${buildDir}/libs/${project.name}.jar")
        File copyTo = file("${projectDir}/src/main/appengine")
        println "Copying $copyFrom to $copyTo"
        copy {
            from copyFrom
            into copyTo
        }
        exec {
            commandLine 'gcloud', 'app', 'deploy'
        }
    }
}